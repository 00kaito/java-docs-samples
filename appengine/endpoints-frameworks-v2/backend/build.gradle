import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:1.0.0'
    classpath 'com.google.cloud.tools:appengine-gradle-plugin:1.3.2'
  }
}

repositories {
    maven {
        url 'https://maven-central.storage.googleapis.com'
    }
    jcenter()
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

def projectId = 'endpointstest2'

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'com.google.cloud.tools.endpoints-framework-server'
apply plugin: 'com.google.cloud.tools.appengine'

dependencies {
    compile 'com.google.endpoints:endpoints-framework:2.0.7'
    compile 'com.google.endpoints:endpoints-management-control-appengine:1.0.3'
    compile 'com.google.endpoints:endpoints-framework-auth:1.0.3'
}

endpointsServer {
  // Endpoints Framework Plugin server-side configuration
  hostname = "echo-api.endpoints.${projectId}.cloud.goog"
}

sourceCompatibility = 1.7  // App Engine Standard uses Java 7
targetCompatibility = 1.7  // App Engine Standard uses Java 7

// this replaces the ${endpoints.project.id} in appengine-web.xml and web.xml
task replaceProjectId(type: Copy) {
    from 'src/main/webapp/WEB-INF/'
    include '*.xml'
    into 'build/exploded-backend/WEB-INF'
    expand(endpoints:[project:[id:projectId]])
    filteringCharset = 'UTF-8'
}
assemble.dependsOn replaceProjectId
